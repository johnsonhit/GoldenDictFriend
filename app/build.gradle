apply plugin: 'com.android.application'

android {
    def keyStoreFileDir = new File(System.getenv("HOME"),
            "Dropbox/development/android/keystore/")
    def keyStorePropFile = new File(keyStoreFileDir, "yang.run.keystore.properties")

    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "run.yang.app.goldendictfriend"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (keyStorePropFile.exists()) {
                def Properties p = new Properties();
                p.load(new FileInputStream(keyStorePropFile));

                storeFile new File(keyStoreFileDir, p['storeFile'])
                storePassword p['storePassword']
                keyAlias p['keyAlias']
                keyPassword p['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (keyStorePropFile.exists()) {
                println "using yang.run's key"
                signingConfig signingConfigs.release
            } else {
                logger.log(LogLevel.ERROR, "ERROR: use debug key in release build")
                signingConfig signingConfigs.debug
            }
        }

        debug {
            applicationIdSuffix ".d"
            versionNameSuffix ".d"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    compile 'com.android.support:support-annotations:25.1.1'
}
